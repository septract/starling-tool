// Allocatable version of Examples/Pass/spinLock.cvf.

typedef int Lock;

pragma grasshopper_include {./grasshopper/spinlock-module.spl};
pragma grasshopper_footprint {LockFoot};
pragma grasshopper_footprint_sort {Set<Lock>};

thread Lock x, ret;
thread bool test;  // Used when trying to take the lock.

view newLock(Lock x), holdLock(Lock x), isLock(Lock x);

method newLock() {
  {| emp |}
    <| ret = %{new Lock}; |>
  {| newLock(ret) |}
    <| %{[|ret|].lock := false}; |>
  {| isLock(ret) |}
}

method lock(Lock x) {
  {| isLock(x) |}
    do {
      {| isLock(x) |}
        test = false;
        <| if (%{ [|x|].lock } == false) {
               test = true;
               %{ [|x|].lock := true };
           } |>
      {| if test == false then isLock(x) else holdLock(x) |}
    } while (test == false);
  {| holdLock(x) |}
}

method unlock(Lock x) {
  {| holdLock(x) |}
    <| %{[|x|].lock := false}; |>
  {| isLock(x) |}
}

method split(Lock x) {
  {| isLock(x) |}
    ;
  {| isLock(x) * isLock(x) |}
}

// False is a hack to implement local variable reasoning.
view False();
constraint False() -> false;

constraint holdLock(x)  ->  %{ [|x|] in LockFoot && [|x|].lock };
constraint isLock(x)    ->  %{ [|x|] in LockFoot };
constraint newLock(x)   ->  %{ [|x|] in LockFoot };

constraint isLock(x)   * newLock(y)   ->  x != y;
constraint holdLock(x) * newLock(y)   ->  x != y;
constraint newLock(x)  * newLock(y)   ->  x != y;

constraint holdLock(x) * holdLock(y)  ->  x != y;
