/*
 * Linux-style ticket lock.
 */

shared int ticket;  // The next ticket to hand out.
shared int serving; // The current ticket holding the lock.
thread int t;  // The thread's current ticket.
thread int s;  // The thread's current view of serving.

/*
 * Locks the ticket lock.
 */
method lock() {
  {| emp |}
    <| t = ticket++; |>
  {| holdTick(t) |}
    do {
      {| holdTick(t) |}
        <| s = serving; |>
      {| if (s == t) { holdLock() } else { holdTick(t) } |}
    } while (s != t);
  {| holdLock() |}
}

/*
 * Unlocks the ticket lock.
 */
method unlock() {
  {| holdLock() |}
    <| s = serving; |>
  {| relLock(s) |}
    <| serving = s + 1; |>
  {| emp |}
}

view holdTick(int t);
view holdLock();
view relLock(int s);

// Invariant
constraint emp                         -> ?;

// Predicate definitions
constraint holdTick(t)                 -> ?;
constraint holdLock()                  -> ?;
constraint relLock(s)                  -> ?;

// Interactions
constraint holdLock()   * holdTick(t)  -> ?;
constraint relLock(s)   * holdTick(t)  -> ?;
constraint holdTick(ta) * holdTick(tb) -> ?;
constraint holdLock()   * holdLock()   -> false;
constraint relLock(s)   * holdLock()   -> ?;
constraint relLock(sa)   * relLock(sb)   -> ?;

