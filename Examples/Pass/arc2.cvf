/* Atomic reference counter
 * Example taken from Derek / Rust: http://www.mpi-sws.org/~dreyer/talks/talk-dagstuhl16.pdf
 * Modified from Examples/Pass/arc.cvf
 * Non-allocated version of Examples/PassGH/arc.cvf
 */

shared bool free;
shared int count;

thread bool f;
thread int c;

view iter arc();
view countCopy(int c);

view error();
constraint error() -> false;

method clone() {
  {| arc() |} 
    < count++ >; 
  {| arc() * arc() |}
}

method print() {
  {| arc() |}
    <f = free>;
  {| if f == true then error() else arc() |}
    if (f == true) {
      // TODO: broken syntax
      {| error() |} 
      ; 
      {| error() |} 
    }
  {| arc() |}
}

method drop() {
  {| arc() |}
    < c = count-- >;
  {| countCopy(c) |}
    if (c == 1) {
      {| countCopy(1) |}
        <free = (true)>;
      {| emp |}
    }
  {| emp |}
}

constraint iter[n] arc() -> 
    n > 0 => (free == false && n <= count);

constraint countCopy(c) ->  
    c == 1 => (free == false && count == 0);

constraint countCopy(m) * countCopy(n) -> 
    (m != 1) || (n != 1);  

