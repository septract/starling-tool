/*
 * Linux-style ticketed lock.
 */

shared int ticket;  // The next ticket to hand out.
shared int serving; // The current ticket holding the lock.
thread int t;  // The thread's current ticket.
thread int s;  // The thread's current view of serving.

/*
 * Locks the ticket lock.
 */
method lock() {
  {| emp |}
    <t = ticket++>;
  {| holdTickLock(t, false) |}
    do {
      {| holdTickLock(t, false) |}
        <s = serving>;
      {| holdTickLock(t, s == t) |}
    } while (s != t);
  {| holdTickLock(t, true) |}
}

/*
 * Unlocks the ticket lock.
 */
method unlock() {
  {| holdTickLock(t, true) |}
    <serving++>;
  {| emp |}
}

view holdTickLock(int t, bool b);

// Invariant
constraint emp                         -> ticket >= serving;

// Predicate definitions
constraint holdTickLock(t, b)          -> ticket > t && (b == false || ticket != serving);

// Interactions
constraint holdTickLock(ta,ba) * holdTickLock(tb,bb) -> ta != tb  && (ba == false || bb == false) 
                                                           && (ba == false || serving != tb)
							   && (bb == false || serving != ta);




/* Comment
   /* Nested Comment */
 */
